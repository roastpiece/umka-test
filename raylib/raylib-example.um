import (
    rl = "raylib.um"
)

fn main() {
    width := 800;
    height := 450;

    rl::InitWindow(width, height, "Umka x Raylib");

    deltaCircle := rl::Vector2 { 0, height/3.0 };
    frameCircle := rl::Vector2 { 0, height*(2.0/3.0) };

    speed := 10.0;
    circleRadius := 10.0;
    currentFps := 60;
    rl::SetTargetFPS(currentFps);

    for !rl::WindowShouldClose() {
        rl::ClearBackground({r: 250, g: 250, b: 250, a: 255});

        mouseWheel := rl::GetMouseWheelMove();
        if (mouseWheel != 0) {
            currentFps += round(mouseWheel);
            if (currentFps < 0) {currentFps = 0;}
            rl::SetTargetFPS(currentFps);
        }

        deltaCircle.x += rl::GetFrameTime() * 6.0 * speed;
        // This circle can move faster or slower visually depending on the FPS
        frameCircle.x += 0.1 * speed;

        // If either circle is off the screen, reset it back to the start
        if (deltaCircle.x > width) {
            deltaCircle.x = 0;
        }
        if (frameCircle.x > width) {
            frameCircle.x = 0;
        }

        mouseX := rl::GetMouseX();
        mouseY := rl::GetMouseY();

        rl::BeginDrawing();
            rl::DrawCircleV(rl::Vector2 {x: real32(mouseX), y: real32(mouseY)}, 4, rl::YELLOW);

            // Draw both circles to the screen
            rl::DrawCircleV(deltaCircle, circleRadius, rl::RED);
            rl::DrawCircleV(frameCircle, circleRadius, rl::BLUE);

            // Draw the help text
            // Determine what help text to show depending on the current FPS target
            fpsText := "";
            if (currentFps <= 0) {
                fpsText = sprintf("FPS: unlimited (%i)", rl::GetFPS());
            } else {
                fpsText = sprintf("FPS: %i (target: %i)", rl::GetFPS(), currentFps);
            }
            rl::DrawText(fpsText, 10, 10, 20, rl::DARKGRAY);
            rl::DrawText(sprintf("Frame time: %02.02f ms", rl::GetFrameTime()), 10, 30, 20, rl::DARKGRAY);
            rl::DrawText("Use the scroll wheel to change the fps limit, r to reset", 10, 50, 20, rl::DARKGRAY);

            // Draw the text above the circles
            rl::DrawText("FUNC: x += GetFrameTime()*speed", 10, 90, 20, rl::RED);
            rl::DrawText("FUNC: x += speed", 10, 240, 20, rl::BLUE);

        rl::EndDrawing();
    }

    rl::CloseWindow();
}
